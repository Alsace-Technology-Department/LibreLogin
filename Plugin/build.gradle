plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'net.kyori.blossom' version '1.3.0'
    id 'java-library'
}

version '0.13.0-DEVELOPMENT'

repositories {
    mavenLocal()
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://repo.opencollab.dev/maven-snapshots/" }
    maven { url = "https://nexus.velocitypowered.com/repository/maven-public/" }
    maven { url = "https://hub.spigotmc.org/nexus/" }
    maven { url = "https://mvn.exceptionflug.de/repository/exceptionflug-public/" }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
}

blossom {
    replaceToken '@version@', version
}

shadowJar {
    archiveFileName = "LibrePremium.jar"

    dependencies {
        exclude(dependency('org.slf4j:.*:.*'))
        exclude(dependency('org.checkerframework:.*:.*'))
        exclude(dependency('com.google.errorprone:.*:.*'))
        exclude(dependency('com.google.protobuf:.*:.*'))
    }

    relocate 'co.aikar.acf', 'xyz.kyngs.librepremiumlib.acf'
    relocate 'com.github.benmanes.caffeine', 'xyz.kyngs.librepremiumlib.caffeine'
    relocate 'com.typesafe.config', 'xyz.kyngs.librepremiumlib.hocon'
    relocate 'com.zaxxer.hikari', 'xyz.kyngs.librepremiumlib.hikari'
    relocate 'com.mysql', 'xyz.kyngs.librepremiumlib.mysql'
    relocate 'org.bstats', 'xyz.kyngs.librepremiumlib.metrics'
    relocate 'org.intellij', 'xyz.kyngs.librepremiumlib.intellij'
    relocate 'org.jetbrains', 'xyz.kyngs.librepremiumlib.jetbrains'
    relocate 'io.leangen.geantyref', 'xyz.kyngs.librepremiumlib.reflect'
    relocate 'org.spongepowered.configurate', 'xyz.kyngs.librepremiumlib.configurate'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    //Velocity
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    compileOnly 'com.velocitypowered:velocity-proxy:3.0.1'

    //MySQL
    implementation 'mysql:mysql-connector-java:8.0.30'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'com.github.kyngs:EasyDB:a4bdf88ee0'

    //ACF
    implementation 'com.github.kyngs.commands:acf-velocity:object-fix-SNAPSHOT'
    implementation 'com.github.kyngs.commands:acf-bungee:object-fix-SNAPSHOT'
    implementation 'com.github.kyngs.commands:acf-paper:object-fix-SNAPSHOT'

    //Utils
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.1'
    implementation 'org.spongepowered:configurate-hocon:4.1.2'
    implementation 'at.favre.lib:bcrypt:0.9.0'
    implementation 'dev.samstevens.totp:totp:1.7.1'
    compileOnly 'dev.simplix:protocolize-api:2.2.2'

    //Geyser
    compileOnly 'org.geysermc.floodgate:api:2.0-SNAPSHOT'

    //Bungeecord
    compileOnly 'net.md-5:bungeecord-api:1.18-R0.1-SNAPSHOT'
    implementation 'net.kyori:adventure-platform-bungeecord:4.1.2'
    compileOnly 'com.github.limework.redisbungee:RedisBungee-API:0.7.2'

    //BStats
    implementation 'org.bstats:bstats-velocity:3.0.0'
    implementation 'org.bstats:bstats-bungeecord:3.0.0'
    implementation 'org.bstats:bstats-bukkit:3.0.0'

    //Paper
    compileOnly 'io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT'
}
